buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://jitpack.io" }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'signing'
apply plugin: 'maven-publish'
apply plugin: 'distribution'
apply plugin: 'groovy'

group = 'com.ullink.slack'
archivesBaseName = 'slack4gerrit'
version = '0.1.0-SNAPSHOT'

jar {
    archiveName = 'slack4gerrit.jar'
    manifest { attributes 'Main-Class': 'com.ullink.slack.review.Connector' }
}

task dist(dependsOn: 'assemble', type: Zip) {
    from('/') {
        include "slack4gerrit.properties"
        into('/')
    }
    from('script') {
        include "*.*"
        into('/')
    }
    from(jar.outputs.files) { into('lib') }
    from(configurations.runtime) { into('lib') }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts { archives javadocJar, sourcesJar }

signing {
    required false
    sign configurations.archives
}

def pomConfig = {
    name 'Slack 4 Gerrit'
    description 'A Slack Bot to share your gerrit review requests'
    url 'https://github.com/Ullink/slack4gerrit'
    licenses {
        license {
            name 'Apache-2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0'
        }
    }
    developers {
        developer {
            id 'bcorne'
            name 'Benoit Corne'
            email 'benoit.corne@gmail.com'
        }
    }
    contributors {
        contributor {
            name 'CappCorp'
            email 'cappcorp@gmail.com'
            url 'https://github.com/CappCorp'
        }
    }
    scm {
        connection 'scm:git:git@github.com:Ullink/slack4gerrit.git'
        developerConnection 'scm:git:git@github.com:Ullink/slack4gerrit.git'
        url 'https://github.com/Ullink/slack4gerrit'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                asNode().children().last() + pomConfig
            }
        }
    }
}

distributions {
    main {
        baseName = 'slack4gerrit'
        contents {
            into('lib') {
                from { configurations.compile }
                from { jar.getOutputs() }
            }
            from { 'script' }
            from { 'slack4gerrit.properties' }
        }
    }
}

dependencies {
    compile 'com.ullink.slack:simpleslackapi:1.4.0'
    compile 'org.glassfish.tyrus.bundles:tyrus-standalone-client:1.13'
    compile 'org.slf4j:slf4j-simple:1.7.7'
    compile 'org.apache.httpcomponents:httpclient:4.5.3'
    compile 'com.google.guava:guava:18.0'
    compile 'com.google.code.gson:gson:2.7'
    compile 'org.mapdb:mapdb:1.0.7'
    compile 'com.google.inject:guice:4.1.0'
    testCompile 'org.codehaus.groovy:groovy-all:2.4.4'
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
    testCompile 'org.objenesis:objenesis:2.1'
    testCompile 'cglib:cglib-nodep:3.2.2'

}
